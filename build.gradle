plugins {
    id 'java-library'
    id 'org.springframework.boot' version '2.3.9.RELEASE'
    id 'pl.allegro.tech.build.axion-release' version '1.12.1'
    id "com.bmuschko.nexus" version "2.3.1"
    id 'io.codearte.nexus-staging' version '0.30.0'
}

apply plugin: 'java-library'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

group = "pl.allegro.tech"
version = scmVersion.version

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = "UTF-8"
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    api 'org.springframework.boot:spring-boot-starter-aop'
    api 'org.springframework.boot:spring-boot-autoconfigure'
    api 'org.slf4j:slf4j-api:1.7.30'
    api('org.apache.curator:curator-recipes:4.2.0')
    api('org.apache.curator:curator-framework:4.2.0') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    testImplementation 'org.springframework.boot:spring-boot'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation platform('org.junit:junit-bom:5.7.1')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    testImplementation platform('org.testcontainers:testcontainers-bom:1.15.2')
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'

    testImplementation 'org.apache.zookeeper:zookeeper:3.6.2'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}

modifyPom {
    project {
        name 'leader-only-spring-boot-starter'
        description 'Spring Boot starter for leader only processing'
        url 'https://github.com/allegro/leader-only-spring-boot-starter'
        inceptionYear '2020'

        scm {
            url 'https://github.com/allegro/leader-only-spring-boot-starter'
            connection 'scm:git@github.com:allegro/leader-only-spring-boot-starter.git'
            developerConnection 'scm:git@github.com:allegro/leader-only-spring-boot-starter.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'wpanas'
                name 'Waldemar Panas'
            }
        }
    }
}

nexusStaging {
    packageGroup = "pl.allegro"

    numberOfRetries = 50
    delayBetweenRetriesInMillis = 5000
}